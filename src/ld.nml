    
/* Begin ld */

/* ITEM*/

//graphics definition
spriteset(spriteset_littleduke_mail, "src/gfx/littleduke_mail.png") {
    tmpl_truck(0, 0)
}

/* Define the road vehicle */
item(FEAT_ROADVEHS, item_ld, 3) {
    property {
        /* Properties common to all vehicle types */
        name:                           string(STR_NAME_LD);
        climates_available:             bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        introduction_date:              date(1998,01,01);
        model_life:                     65;
        /* retire_early not set, use default retirement behaviour */
        vehicle_life:                   40;
        reliability_decay:              20;
        refittable_cargo_classes:       bitmask(CC_MAIL);
        default_cargo_type:             MAIL;
        non_refittable_cargo_classes:   bitmask(CC_PASSENGERS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:                  50;
        cost_factor:                    200;
        running_cost_factor:            200;
        /* cargo_age_period is left at default */

        /* RV-specific properties */
        sprite_id:                      SPRITE_ID_NEW_ROADVEH;              //enable new graphics
        speed:                          144 km/h;
        misc_flags:                     bitmask(ROADVEH_FLAG_2CC);
        refit_cost:                     0;                                  // Refitting is free
        /* callback_flags are not set, no need to manually enable callbacks */
        running_cost_base:              RUNNING_COST_ROADVEH;
        power:                          380 hp;
        weight:                         10 ton;
        tractive_effort_coefficient:    0.7;                                // Default is 0.3
        /* Air drag coefficient is left at default */
        cargo_capacity:                 250;                                // Max 255. Overridden by callback below
        sound_effect:                   SOUND_BUS_START_PULL_AWAY;
        /* Visual effect is left at default (no effect) */
    }

    /* Define graphics */
    graphics {
        default:                        spriteset_littleduke_mail; 
        cargo_capacity:                 500;                                // This will be what appears in game
    }
}

/* End ld */
